let types @ {
    TransactionBlockResponse,
    ObjectChange,
    SequenceNumber,
    ObjectDigest,
    Owner,
    ObjectID,
    ?
} = import! lancer.rpc.types

let { Digest, ? } = import! lancer.sui.digest
let { SuiAddress, ? } = import! lancer.sui.sui_address
let { Serialize, ? } = import! std.json.ser
let { Show, show } = import! std.show
let { (<<) } = import! std.function
let prim @ { TransactionBlockResponsePtr } = import! lancer.rpc.prim

#[derive(Show, Eq)]
type ObjectChangeKind =
    // Module published
    | PublishedKind
    // Transfer objects to new address / wrap in another object
    | TransferredKind
    // Object mutated.
    | MutatedKind
    // Delete object
    | DeletedKind
    // Wrapped object
    | WrappedKind
    // New object creation
    | CreatedKind

let object_change_kind c : ObjectChange -> ObjectChangeKind =
    match c with
    | Published _ -> PublishedKind
    | Transferred _ -> TransferredKind
    | Mutated _ -> MutatedKind
    | Deleted _ -> DeletedKind
    | Wrapped _ -> WrappedKind
    | Created _ -> CreatedKind

let object_change_id c : ObjectChange -> ObjectID =
    match c with
    | Published p -> p.package_id
    | Transferred t -> t.object_id
    | Mutated m -> m.object_id
    | Deleted d -> d.object_id
    | Wrapped w -> w.object_id
    | Created c -> c.object_id

let object_change_version c : ObjectChange -> SequenceNumber =
    match c with
    | Published p -> p.version
    | Transferred t -> t.version
    | Mutated m -> m.version
    | Deleted d -> d.version
    | Wrapped w -> w.version
    | Created c -> c.version

let object_change_digest c : ObjectChange -> Option ObjectDigest =
    match c with
    | Published p -> Some p.digest
    | Transferred t -> Some t.digest
    | Mutated m -> Some m.digest
    | Deleted d -> None
    | Wrapped w -> None
    | Created c -> Some c.digest

let object_change_owner c : ObjectChange -> Option Owner =
    match c with
    | Published p -> None
    | Transferred t -> Some t.recipient
    | Mutated m -> Some m.owner
    | Deleted d -> None
    | Wrapped w -> None
    | Created c -> Some c.owner

let inner : TransactionBlockResponsePtr -> TransactionBlockResponse = prim.inner

let from_inner : TransactionBlockResponse -> TransactionBlockResponsePtr = prim.from_inner

let seriazlize_TransactionBlockResponseRef : Serialize TransactionBlockResponsePtr = {
    serialize = prim.serialize,
}

let show_TransactionBlockResponseRef : Show TransactionBlockResponsePtr = {
    show = prim.show,
}

let seriazlize_TransactionBlockResponse : Serialize TransactionBlockResponse = {
    serialize = prim.serialize << from_inner,
}

let show_TransactionBlockResponse : Show TransactionBlockResponse = {
    show = prim.show << from_inner,
}

{
    TransactionBlockResponsePtr,
    ObjectChangeKind,
    object_change_kind,
    show_ObjectChangeKind,
    eq_ObjectChangeKind,
    object_change_id,
    object_change_version,
    object_change_digest,
    object_change_owner,
    seriazlize_TransactionBlockResponseRef,
    show_TransactionBlockResponseRef,
    seriazlize_TransactionBlockResponse,
    show_TransactionBlockResponse,
    inner,
    from_inner,
    .. types
}
